//
// HOW TO USE
//
// Run the script (loop mode not necessary) and target a container holding leather and sewing kits
//
unsetalias 'tool'
unsetalias 'stockpile'
if not listexists 'recycle'
  createlist 'recycle'
endif
if not listexists 'recyclables'
  createlist 'recyclables'
endif
if not listexists 'tooltype'
  createlist 'tooltype'
endif
if not listexists 'resource'
  createlist 'resource'
endif
clearlist 'recyclables'
clearlist 'recycle'
clearlist 'tooltype'
clearlist 'resource'
// set up script for tailoring
pushlist 'tooltype' 0xf9d // tailoring kit
pushlist 'resource' 0x1081 // leather
pushlist 'recyclables' 0x13c7 // leather gorget
pushlist 'recyclables' 0x13ce // leather gloves
pushlist 'recyclables' 0x1dba // leather cap
pushlist 'recyclables' 0x13d6 // studded gorget
pushlist 'recyclables' 0x13dd // studded gloves
pushlist 'recyclables' 0x1dba // studded cap
pushlist 'recyclables' 0x13d4 // studded arms
pushlist 'recyclables' 0x13e1 // studded legs
pushlist 'recyclables' 0x1eba // tailoring repair kit
//
promptalias 'stockpile' 'Select the container that has your material and tools'
//
// loop
//
removetimer 'totaltime'
createtimer 'totaltime'
while not dead
  //
  // check for tools
  //
  if not @findtype tooltype[0] 'any' 'backpack'
    if @findtype tooltype[0] 'any' 'stockpile'
      for 5
        @movetype tooltype[0] 'stockpile' 'backpack' 0 0 0 'any'
        pause 1200
      endfor
    else
      headmsg 'Script: Not enough tools in stockpile or backpack.'
      timermsg 'totaltime' 88
      stop
    endif
  endif
  //
  // recycling function
  //
  if contents 'backpack' >= 125
    pushlist 'recycle' 1
  elseif weight >= maxweight
    pushlist 'recycle' 1
  elseif @counttype resource[0] 'any' 'backpack' < 20
    pushlist 'recycle' 1
  endif
  if list 'recycle' > 0
    sysmsg "Recycling"
    clearlist 'recycle'
    if @findtype tooltype[0] 'any' 'backpack'
      // Grab the tool.
      @setalias 'tool' 'found'
      for 0 to 'recyclables'
        sysmsg "Checking Recyclable"
        if @findtype recyclables[] 'any' 'backpack'
          // Found items to recycle
          sysmsg "Found items to recycle"
          useobject 'tool'
          waitforgump 0x38920abd 15000
          replygump 0x38920abd 3
          waitfortarget 15000
          target! 'found'
          pause 200
        endif
      endfor
    else
      headmsg 'Script Stopped: You need atleast 20 of your resource, and you have no tools.'
      timermsg 'totaltime' 88
      stop
    endif
  endif
  //
  // Check for materials
  //
  if @counttype resource[0] 0 'backpack' < 20
    sysmsg "Need more resources"
    if @findtype resource[0] 0 'stockpile'
      sysmsg "Getting resources from stockpile"
      @removetimer 'restock'
      createtimer 'restock'
      while @findtype resource[0] 0 'stockpile'
        if amount 'found' < 200
          sysmsg "Moving all of a small stack"
          @moveitem 'found' 'backpack' 0 0 0 'any'
          pause 1500
        else
          sysmsg "moving leather"
          @moveitem 'found' 'backpack' 0 0 0 200
          pause 1500
        endif
        if timer 'restock' > 30000
          sysmsg "Unable to restock leather" 44
          timermsg 'totaltime' 88
          stop
        endif
        if @counttype resource[0] 0 'backpack' >= 200
          break
        endif
      endwhile
    else
      headmsg 'Script: Not enough leather in stockpile or backpack.'
      timermsg 'totaltime' 88
      stop
    endif
  endif
  //
  // Training
  //
  sysmsg "Training"
  if not @findobject 'tool' 'any' 'backpack'
    @findtype tooltype[0] 'any' 'backpack'
    @setalias 'tool' 'found'
    @useobject 'tool'
    waitforgump 0x38920abd 15000
  endif
  if not @gumpexists 0x38920abd
    @useobject 'tool'
    waitforgump 0x38920abd 15000
  endif
  //if not @gumpexists 0x38920abd or @injournal 'worn out your tool' 'system'
  //  @clearjournal
  //  @useobject 'tool'
  //  waitforgump 0x38920abd 15000
  //endif
  while not @ingump 0x38920abd "Recycle All (of Type)"
    replygump 0x38920abd 2
    waitforgump 0x38920abd 15000
    pause 150
  endwhile
  if skill 'Tailoring' < 65
    if not @ingump 0x38920abd 'Leather Gorget'
      replygump 0x38920abd 101
      waitforgump 0x38920abd 15000
    endif
    replygump 0x38920abd 200
  elseif skill 'Tailoring' < 70
    if not @ingump 0x38920abd 'Leather Gorget'
      replygump 0x38920abd 101
      waitforgump 0x38920abd 15000
    endif
    replygump 0x38920abd 201
  elseif skill 'Tailoring' < 75
    if not @ingump 0x38920abd 'Leather Gorget'
      replygump 0x38920abd 101
      waitforgump 0x38920abd 15000
    endif
    replygump 0x38920abd 202
  elseif skill 'Tailoring' < 85
    if not @ingump 0x38920abd 'Studded Gorget'
      replygump 0x38920abd 102
      waitforgump 0x38920abd 15000
    endif
    replygump 0x38920abd 200
  elseif skill 'Tailoring' < 90
    if not @ingump 0x38920abd 'Studded Gorget'
      replygump 0x38920abd 102
      waitforgump 0x38920abd 15000
    endif
    replygump 0x38920abd 201
  elseif skill 'Tailoring' < 95
    if not @ingump 0x38920abd 'Studded Gorget'
      replygump 0x38920abd 102
      waitforgump 0x38920abd 15000
    endif
    replygump 0x38920abd 202
  elseif skill 'Tailoring' < 100
    if not @ingump 0x38920abd 'Studded Gorget'
      replygump 0x38920abd 102
      waitforgump 0x38920abd 15000
    endif
    replygump 0x38920abd 203
  else
    sysmsg "Finished!"
    timermsg 'totaltime' 88
    stop
  endif
  waitforgump 0x38920abd 15000
  waitforgump 0x38920abd 15000
endwhile
